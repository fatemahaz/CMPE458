% PT Scanner.ssl   Ver. 4.2
%
% Date:  30 Jun 1980
%       (Revised v4.2 - 18 Dec 2019)
%       (Revised Qust - 27 Dec 2019)
%
% Author:  Alan Rosselet
%
% Abstract
%     The scanner breaks a PT source program up into syntax tokens.
%   It removes token separators - blanks, tabs, comments.  The
%   scanner detects these lexical errors:  unclosed comments,
%   unclosed literals, literals that cross a line boundary,
%   illegal characters and integers followed by identifiers with no
%   separator between them.

Input : 
        lLetter 
        firstCharClass = lLetter 
        lDigit
        lPlus           '+'
        lMinus          '-'
        lStar           '*'
        lColon          ':'
        lEquals         '='
        lDot            '.'
        lComma          ','
        lSemicolon      ';'
        lQuote
        lBackslash
        lLeftAngle      '<'
        lRightAngle     '>'
        lLeftParen      '('
        lRightParen     ')'
        lLeftBracket    '['
        lRightBracket   ']'
        lLeftBrace      '{'
        lRightBrace     '}'
%% ADDED
        lQuestionMark   '?'
        lSlash          '/'
        lPercent        '%'
        lAmpersand      '&'
        lOrBar          '|'
        lExclamation    '!'
        lUnderscore     '_'
%%
        lBlank
        lIllegal
        lNewLine
        lCarriageReturn
        lTab
        lNewPage 
        lEndFile
        lastCharClass = lEndFile;

Output :
        % Keyword tokens -
        %   Tokens pDiv through pRepeat are keywords which will be separated
        %   from user defined identifiers by the Screener.  This Scanner
        %   treats them all as identifiers.
%% CHANGED
%%      pDiv 
%%      firstKeywordToken = pDiv
%%      pMod
%%      pOr
%%      pAnd
%%      pNot
%%      pThen
%%      pElse
%%      pOf
%%      pEnd
%%      pUntil
%%      pDo
%%      pArray
%%      pFile
%%      pProgram
%%      pConst
%%      pVar
%%      pType
%%      pProcedure
%%      pBegin
%%      pIf
%%      pCase
%%      pWhile
%%      pRepeat
%%      lastKeywordToken = pRepeat
        pElse
        firstKeywordToken = pElse
        pOf
        pFile
        pType
        pPub
        pIf
        pWhile
        pMod
        pMain
        pConst
        pLet
        pMut
        pFn
        pLoop
        pBreak
        pMatch
        lastKeywordToken = pMatch
%% 

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus
        pMinus
        pStar
%% DELETED
%%      pColonEquals
%%
        pDot
        pComma
        pSemicolon
        pColon
        pEquals
        pNotEqual
        pLess
        pLessEqual
        pGreaterEqual
        pGreater
        pLeftParen
        pRightParen
        pLeftBracket
        pRightBracket
        pDotDot
%% CHANGED
%%      lastSyntaxToken = pDotDot;
        pLeftBrace
        pRightBrace
        pPlusEquals
        pMinusEquals
        pSlash
        pPercent
        pDoubleEquals
        pDoubleAmpersand
        pDoubleOrBar
        pOrBar
        pNot
        pUnderscore
        pQuestionMark
        pEqualGreater
        lastSyntaxToken = pEqualGreater;
%% 

Error :
        eIllegalChar = 5
        firstScanError = eIllegalChar
        eStringLiteralLine
        eStringLiteralEOF
        eCommentEOF
        eNoSeparator
        lastScanError = eNoSeparator;

mechanism Buffer :
    % Buffer saves the characters associated with the
    % compound tokens identifier, integer and literal.

    oBufferSave;
        % Save the character form of the last accepted lexical token.




rules

% Note that the order of choice alternatives
% may have a significant effect on performance
% under some implementations of S/SL.
Scan :
        {[
            | lBlank :
            | lLetter:
                oBufferSave
                @Identifier
            | lNewLine:
                .pNewLine
            | lCarriageReturn:
                % from Windows files, ignore
            | lTab:
            | ';':
                .pSemicolon
%% CHANGED
%%          | '(':
%%              [
%%                  | '*':
%%                      @AlternateComment
%%                  | *:
%%                      .pLeftParen
%%              ]
            | '(':
                .pLeftParen
%% 
            | ')':
                .pRightParen
            | '[':
                .pLeftBracket
            | ']':
                .pRightBracket
            | ',':
                .pComma
%% CHANGED
%%          | ':':
%%              [
%%                  | '=':
%%                      .pColonEquals
%%                  | *:
%%                      .pColon
%%              ]
            | ':':
                .pColon
%%
            | lDigit:
                oBufferSave
                @Integer
                [
                    | lLetter:
                        #eNoSeparator
                        oBufferSave
                        @Identifier
                    | *:
                ]
%% CHANGED
%%          | '=':
%%              .pEquals
%%          | '+':
%%              .pPlus
            | '=':
                [
                    | '>':
                        .pEqualGreater
                    | '=':
                        .pDoubleEquals
                    | *:
                        .pEquals
                ]
            | '+':
                [
                    | '=':
                        .pPlusEquals
                    | *:
                        .pPlus
                ]
%% 
            | lQuote:
                @StringLiteral
%% CHANGED
%%          | '-':
%%              .pMinus
            | '-':
                [
                    | '=':
                        .pMinusEquals
                    | *:
                        .pMinus
                ]
%% 
            | '.':
                [
                    | '.':
                        .pDotDot
                    | *:
                        .pDot
                ]
            | '<':
                [
%% DELETED
%%                  | '>':
%%                      .pNotEqual
%% 
                    | '=':
                        .pLessEqual
                    | *:
                        .pLess
                ]
            | '>':
                [
                    | '=':
                        .pGreaterEqual
                    | *:
                        .pGreater
                ]
%% CHANGED
%%          | '{':
%%              @Comment
            | '{':
                .pLeftBrace
%%
            | '*':
                .pStar
%% ADDED
            | '}':
                .pRightBrace
            | '/':
                [
                    | '/':
                        @Comment
                    | '*':
                        @AlternateComment
                    | *:
                        .pSlash
                ]
            | '%':
                .pPercent
            | '!':
                [
                    | '=':
                        .pNotEqual
                    | *:
                        .pNot
                ]
            | '&':
                [
                    | '&':
                        .pDoubleAmpersand
                    | *:
                        #eIllegalChar
                ]
            | '|':
                [
                    | '|':
                        .pDoubleOrBar
                    | *:
                        .pOrBar
                ]
            | '_':
                .pUnderscore
            | '?':
                .pQuestionMark
%% 
            | lNewPage:
            | lEndFile:
                .pEndFile
                >
            | *:
                ?
                #eIllegalChar
        ]};

Identifier :
        % Assemble an identifier token

        {[
            | lLetter, lDigit:
                oBufferSave
            | *:
                .pIdentifier
                >
        ]};

Integer :
        % Assemble an integer token

        {[
            | lDigit:
                oBufferSave
            | *:
                .pInteger
                >
        ]};

StringLiteral :
        % Assemble a literal string token

        {[
            | lQuote:
                [
                    | lQuote:
                        oBufferSave
                    | *:
                        .pStringLiteral
                        >
                ]
            | lNewLine:         % literal must not cross a line boundary
                #eStringLiteralLine
                .pNewLine
                >
            | lEndFile:         % literal must be closed before EOF
                #eStringLiteralEOF
                .pEndFile
                >
            | *:        % literal may contain any other character
                [
                    | lBackslash:
                        oBufferSave
                    | *:
                ]
                ?
                oBufferSave
        ]};

Comment :
        % Discard the contents of a comment

        {[
%% CHANGED
%%          | '}':
%%              >
            | lNewLine:
                .pNewLine
                >
%% 
            | lEndFile:         % comment must be closed before EOF
                #eCommentEOF
                .pEndFile
                >
            | *:        % comment may contain any other character
                ?
        ]};

AlternateComment :
        {[
            | '*':
                [
%% CHANGED
%%                  | ')':
%%                      >
                    | '/':
                        >
%%
                    | *:
                ]
            | lNewLine:
                .pNewLine
            | lEndFile:
                #eCommentEOF
                .pEndFile
                >
            | *:
                ?
        ]};

end
