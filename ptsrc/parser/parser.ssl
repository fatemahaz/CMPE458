%  PT Parser.ssl        Ver. 4.2
%
% Date:  30 Jun 1980
%        (Revised v4.2 - 18 Dec 2019)
%        (Revised Qust - 27 Dec 2019)
%
% Author:  Alan Rosselet
%
% Abstract
%
%     This module parses the syntax token stream emitted by the scanner
%   and emits a postfix stream of tokens to the semantic analyzer
%   module.  The parser detects and repairs syntax errors so that the
%   token stream emitted to the semantic analyzer is syntactically
%   legal (except for the special case of declaration phase errors - see
%   comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

%% CHANGED
%%      pDiv                    'div'
%%      firstKeywordToken = pDiv
%%      pMod                    'mod'
%%      pOr                     'or'
%%      pAnd                    'and'
%%      pNot                    'not'
%%      pThen                   'then'
%%      pElse                   'else'
%%      pOf                     'of'
%%      pEnd                    'end'
%%      pUntil                  'until'
%%      pDo                     'do'
%%      pArray                  'array'
%%      pFile                   'file'
%%      pProgram                'program'
%%      pConst                  'const'
%%      pVar                    'var'
%%      pType                   'type'
%%      pProcedure              'procedure'
%%      pBegin                  'begin'
%%      pIf                     'if'
%%      pCase                   'case'
%%      pWhile                  'while'
%%      pRepeat                 'repeat'
%%      lastKeywordToken = pRepeat 
        pElse                   'else'
        firstKeywordToken = pElse
        pOf                     'of'
        pFile                   'file'
        pType                   'type'
        pPub                    'pub'
        pIf                     'if'
        pWhile                  'while'
        pMod                    'mod'
        pMain                   'main'
        pConst                  'const'
        pLet                    'let'
        pMut                    'mut'
        pFn                     'fn'
        pLoop                   'loop'
        pBreak                  'break'
        pMatch                  'match'
        lastKeywordToken = pMatch
%%

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
%% DELETED
%%      pColonEquals            ':='
%%
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
%% CHANGED
%%      pNotEqual               '<>'
        pNotEqual               '!='
%%
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pDotDot                 '..'
%% CHANGED
%%      lastSyntaxToken = pDotDot;
        pLeftBrace              '{'
        pRightBrace             '}'
        pPlusEquals             '+='
        pMinusEquals            '-='
        pSlash                  '/'
        pPercent                '%'
        pDoubleEquals           '=='
        pDoubleAmpersand        '&&'
        pDoubleOrBar            '||'
        pOrBar                  '|'
        pNot                    '!'
        pUnderscore             '_'
        pQuestionMark           '?'
        pEqualGreater           '=>'
        lastSyntaxToken = pEqualGreater;
%%

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        sType
        sVar
%% ADDED
        sMutable
        sInitialValue
        sPublic
        sModule
%%
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
%% ADDED
        sCaseOtherwise
%%
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
%% CHANGED
%%      sRepeatStmt
%%      sRepeatEnd
        sLoopStmt
        sLoopEnd
        sLoopBreakIf
%%
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
%% ADDED
        sSubstring
        sLength
%%
        sNewLine
        sEndOfFile
        lastSemanticToken = sEndOfFile;


rules

Program :
%% CHANGED
%%      'program'  .sProgram
%%      % program name
%%      pIdentifier
        'mod' .sProgram
        'main'
%%
        % program parameters
        '('
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }
        ')'
        .sParmEnd
%% CHANGED
%%      ';'  @Block  '.';
        '{' 
            @Block 
        '}';
%%

Block :
        % Strictly speaking, standard Pascal requires that declarations
        % appear only in the order consts, types, vars, procedures.
        % We allow arbitrary intermixing of these instead to preserve
        % programmer sanity.
%% ADDED
        .sBegin
%%
        {[
            | 'const':
                .sConst
                @ConstantDefinitions
            | 'type':
                .sType
                @TypeDefinitions
%% CHANGED
%%          | 'var':
            | 'let':
%%
                .sVar
                @VariableDeclarations
%% CHANGED
%%          | 'procedure':
%%              .sProcedure
%%              % procedure name
%%              pIdentifier  .sIdentifier
%%              @ProcedureHeading
%%              @Block  ';'
            | 'fn':
                .sProcedure
                pIdentifier  .sIdentifier
                @ProcedureHeading
                '{'
                    @Block  
                '}'
            | 'pub':
                'fn'
                .sProcedure
                pIdentifier  .sIdentifier
                .sPublic
                @ProcedureHeading
                '{'
                    @Block  
                '}'
            | 'mod':
                .sModule
                pIdentifier  .sIdentifier
                '{'
                    @Block  
                '}'
%%
%% ADDED
	    % From old PT Statement rule
            | pIdentifier:
                @AssignmentOrCallStmt
            | 'if':
                @IfStmt
            | 'while':
                @WhileStmt
            | 'loop':
                @LoopStmt
            | 'match':
                @CaseStmt
            | ';':
                .sNullStmt
%%
            | *:
                >
        ]}
%% CHANGED
%%      'begin'
%%      @BeginStmt;
        .sEnd;
%%
        
ConstantDefinitions :
        % Accept one or more named constant definitions
%% CHANGED
%%      pIdentifier  .sIdentifier
%%      '=' @ConstantValue ';'
%%      {[
%%          | pIdentifier:
%%              .sIdentifier
%%              '=' @ConstantValue ';'
%%          | *:
%%              >
%%      ]};
        {
            pIdentifier  .sIdentifier
            '=' @ConstantValue 
            [
                | ',':
                | *:
                    >
            ]
        }
        ';' ;
%%

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

TypeDefinitions :
%% CHANGED
%%      % Accept one or more named type definitions.
%%      pIdentifier  .sIdentifier
%%      '='  @TypeBody  ';'
%%      {[
%%          | pIdentifier:
%%              .sIdentifier
%%              '='  @TypeBody  ';'
%%          | *:
%%              >
%%      ]};
        % Accept one named type definition.
        pIdentifier  .sIdentifier
        '='  @TypeBody  ';' ;
%%

TypeBody :
        [
%% CHANGED
%%          | 'array':
%%              .sArray
%%              '['  @SimpleType  ']'
%%              'of'  @SimpleType
            | '[':
                .sArray
                @SimpleType  
                ':'
                .sRange
                @UnsignedIntegerConstant
                ']'
%%
            | 'file':
                .sFile
                'of'  @SimpleType
            | *:
                @SimpleType
        ];

SimpleType :
%% CHANGED
%%      % A simple type is a named type (possibly predefined)
%%      % or a subrange type specification
%%      [
%%          | pIdentifier:
%%              .sIdentifier
%%              [
%%                  | '..':
%%                      .sRange         %% this may be unnecessary
%%                      @OptionallySignedIntegerConstant
%%                  | *:
%%              ]
%%          | *:
%%              @OptionallySignedIntegerConstant
%%              '..'  .sRange
%%              @OptionallySignedIntegerConstant
%%      ];
        % In Qust, a simple type is just a named type (possibly predefined)
        pIdentifier .sIdentifier;
%%

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

VariableDeclarations :
        % Accept one or more variable declarations.
%% CHANGED
%%      pIdentifier  .sIdentifier
%%      ':'  @TypeBody  ';'
%%      {[
%%          | pIdentifier:
%%              .sIdentifier
%%              ':'  @TypeBody  ';'
%%          | *:
%%              >
%%      ]};
        {
            [
                | 'mut':
                    pIdentifier  .sIdentifier
                    @TypeOrValue
                    .sMutable
                | *:
                    pIdentifier  .sIdentifier
                    @TypeOrValue
            ]
            [
                | ',':
                | *:
                    >
            ]
        }
        ';' ;
%%

%% ADDED
TypeOrValue:
        [
            | ':':
                @TypeBody
                [
                    | '=':
                        .sInitialValue
                        @Expression
                        .sExpnEnd
                    | *:
                ]
            | '=':
                .sInitialValue
                @Expression
                .sExpnEnd
        ];
%%

ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
%% CHANGED
%%      [
%%          | '(':
%%              {
%%                  % formal parameter identifier
%%                  [
%%                      | 'var':
%%                          pIdentifier  .sIdentifier
%%                          .sVar
%%                      | *:
%%                          pIdentifier  .sIdentifier
%%                  ]
%%                  ':'
%%                  % type identifier
%%                  pIdentifier  .sIdentifier
%%                  [
%%                      | ';':
%%                      | *:
%%                          >
%%                  ]
%%              }
%%              ')'
%%          | *:
%%      ]
%%      ';'
%%      .sParmEnd;
        '('
        [
            | ')':
                % no parameters
            | *:
                {
                    % formal parameter identifier
                    [
                        | 'mut':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    ':'
                    % type identifier
                    pIdentifier  .sIdentifier
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
        ]
        .sParmEnd;
%%

%% DELETED (merged into Block above) 
%% Statement :
%%       [
%%           | pIdentifier:
%%               @AssignmentOrCallStmt
%%           | 'begin':
%%               @BeginStmt
%%           | 'if':
%%               @IfStmt
%%           | 'while':
%%               @WhileStmt
%%           | 'case':
%%               @CaseStmt
%%           | 'repeat':
%%               @RepeatStmt
%%           | *:
%%               .sNullStmt
%%       ];
%%

AssignmentOrCallStmt :
        [
%% CHANGED
%%          | ':=':
            | '=':
%%
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
%% CHANGED
%%              ']'  ':='
                ']'  '='
%%
                @Expression
                .sExpnEnd
%% ADDED
            | '+=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier    
                @Expression
                .sAdd
                .sExpnEnd
            | '-=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sSubtract
                .sExpnEnd
%%
            | *:
                @CallStmt
        ] 
	';' ;

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
%% ADDED
        '('
%%
        [
%% CHANGED
%%          | '(':
            | ')':
                % empty argument list
            | *:
%%
                {
%% ADDED
                    [
                        | 'mut':
                            .sMutable
                        | *:
                    ]
%%
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
%% DELETED
%%          | *:        % no actual parameters
%%
        ]
        .sParmEnd;

%% DELETED
%% BeginStmt :
%%      .sBegin
%%      {
%%          @Statement
%%          [
%%              | ';':
%%              | 'end':
%%                  .sEnd
%%                  >
%%          ]
%%      };
%%

IfStmt :
        .sIfStmt
        @Expression
        .sExpnEnd
%% CHANGED
%%      'then'  .sThen
%%      @Statement
        .sThen
        '{'
            @Block
        '}'
%%
        [
            | 'else':
                .sElse
%% CHANGED
%%              @Statement
                [
                    | 'if':
                        .sBegin
                        @IfStmt
                        .sEnd
                    | *:
                        '{'
                            @Block
                        '}'
                ]
%%
            | *:
        ];

CaseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
%% CHANGED
%%      'of'
%%      @CaseAlternative
%%      {[
%%          | ';':
%%              % since case alternatives cannot be null, allow
%%              % for the common situation of an extra semicolon
%%              % at the end of the list of case alternatives.
%%              [
%%                  | 'end':
%%                      >
%%                  | *:
%%                      @CaseAlternative
%%              ]
%%          | 'end':
%%              >
%%      ]}
        '{'
            '|' @CaseAlternative
            {[
                | '|':
                    [
                        | '_':
                            .sCaseOtherwise
                            '=>'
                            '{'
                                @Block
                            '}'
                            >
                        | *:
                            @CaseAlternative
                    ]
                | *:
                    >
            ]}
        '}'
%%
        .sCaseEnd;

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants

        {
            @OptionallySignedIntegerConstant
            [
%% CHANGED
%%              | ',':
                | '|':
%%
                | *:
                    >
            ]
        }
        .sLabelEnd
%% CHANGED
%%      ':'  @Statement;
        '=>'
        '{'
            @Block
        '}';
%%

WhileStmt :
        .sWhileStmt
        @Expression
        .sExpnEnd
%% CHANGED
%%      'do'
%%      @Statement;
        '{'
            @Block
        '}';
%%

%% CHANGED
%% RepeatStmt :
%%      .sRepeatStmt
%%      {
%%          @Statement
%%          [
%%              | ';':
%%              | 'until':
%%                  .sRepeatEnd
%%                  >
%%          ]
%%      }
%%      @Expression
%%      sExpnEnd;
%%
LoopStmt :
        .sLoopStmt
        '{'
            @Block
            'break' 'if' .sLoopBreakIf @Expression .sExpnEnd ';'
            @Block
        '}'
	.sLoopEnd;
%%

Expression :
        @SimpleExpression
        [
%% CHANGED
%%          | '=':
            | '==':
%%
                @SimpleExpression  .sEq
%% CHANGED
%%          | '<>':
            | '!=':
%%
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
%% CHANGED
%%          | 'or':
            | '||':
%%
                .sInfixOr  @Term  .sOr
            | *:
                >
        ]};

Term :
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
%% CHANGED
%%          | 'div':
%%              @Factor  .sDivide
            | '/':
                @Factor  
                [
                    | ':':
                        @Factor
                        .sSubstring
                    | *:
                        .sDivide
                ]
%% CHANGED
%%          | 'mod':
            | '%':
%%
                @Factor  .sModulus
%% CHANGED
%%          | 'and':
            | '&&':
%%
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
%% CHANGED
%%          | 'not':
            | '!':
%%
                @Factor
                .sNot
%% ADDED
            | '?':
                @Factor
                .sLength
%%
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
